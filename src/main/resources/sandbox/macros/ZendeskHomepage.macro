definition {
	var zendeskURL = PropsUtil.get('zendesk.liferay.url');

	@prose = "I log into zendesk as ${userEmailAddress} with password ${userPassword}"
	macro login {
		Open(locator1 = "${zendeskURL}");

		Click(locator1 = "ZendeskHomepage#SIGN_IN_BUTTON");

		Type(
			locator1 = "ZendeskSignIn#EMAIL_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "ZendeskSignIn#PASSWORD_FIELD",
			value1 = "${userPassword}");

		Check(locator1 = "ZendeskSignIn#REMEMBER_ME_CHECKBOX");

		Click(locator1 = "ZendeskSignIn#SIGN_IN_BUTTON");
	}

	@prose = "I log out of zendesk"
	macro logout {
		Open(locator1 = "${zendeskURL}");

		Click(locator1 = "ZendeskHomepage#USER_MENU_BUTTON");
		Click(locator1 = "ZendeskHomepage#USER_MENU_SIGN_OUT_LINK");
	}

	@prose = "I log out and log into zendesk as ${userEmailAddress} with password ${userPassword}"
	macro logoutAndLogin {
		ZendeskHomepage.logout();

		ZendeskHomepage.login(
			userEmailAddress = "${userEmailAddress}",
			userPassword = "${userPassword}");
	}

	@prose = "I should see the user dropdown button"
	macro viewLoggedIn {
		AssertElementPresent(locator1 = "ZendeskHomepage#USER_MENU_BUTTON");
		AssertElementNotPresent(locator1 = "ZendeskHomepage#SIGN_IN_BUTTON");
	}
}