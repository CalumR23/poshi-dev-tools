definition {
	@prose = "I create a ticket with the following values ${table}"
	macro create {
		Click(locator1 = "ZendeskHomepage#SUBMIT_A_TICKET_BUTTON");

		var RawTable rawTable = new RawTable("${table}");

		for (var row : table "${rawTable}") {
			var field = "${row[0]}";
			var value = "${row[1]}";
			var subValue = "${row[2]}";

			if ("${field}" == "Organization") {
				Click(locator1 = "ZendeskTicketEndUserAdd#ORGANIZATION_DROPDOWN_LINK");
				Click(
					locator1 = "ZendeskTicketEndUserAdd#OPTION",
					key_option = "${value}");
			}
			else if ("${field}" == "Product") {
				Click(locator1 = "ZendeskTicketEndUserAdd#PRODUCT_DROPDOWN_LINK");
				Click(
					locator1 = "ZendeskTicketEndUserAdd#OPTION",
					key_option = "${value}");
				if (!("${subValue}" == "")) {
					Click(
						locator1 = "ZendeskTicketEndUserAdd#OPTION",
						key_option = "${subValue}");
				}
			}
			else if ("${field}" == "Component") {
				Click(locator1 = "ZendeskTicketEndUserAdd#COMPONENT_DROPDOWN_LINK");
				Click(
					locator1 = "ZendeskTicketEndUserAdd#OPTION",
					key_option = "${value}");
			}
			else if ("${field}" == "System Status") {
				Click(locator1 = "ZendeskTicketEndUserAdd#SYSTEM_STATUS_DROPDOWN_LINK");
				Click(
					locator1 = "ZendeskTicketEndUserAdd#OPTION",
					key_option = "${value}");
				if (!("${subValue}" == "")) {
					Click(
						locator1 = "ZendeskTicketEndUserAdd#OPTION",
						key_option = "${subValue}");
				}
			}
			else if ("${field}" == "Subject") {
				Type(
					locator1 = "ZendeskTicketEndUserAdd#SUBJECT_FIELD",
					value1 = "${value}");
			}
			else if ("${field}" == "Description") {
				Type(
					locator1 = "ZendeskTicketEndUserAdd#DESCRIPTION_FIELD",
					value1 = "${value}");
			}
			else if ("${field}" == "Attachments") {
				UploadCommonFile(
					locator1 = "ZendeskTicketEndUserAdd#ATTACHMENTS_FIELD",
					value1 = "${value}");
			}
			else {
				fail("No such ticket field ${field}");
			}
		}
		Click(locator1 = "ZendeskTicketEndUserAdd#SUBMIT_BUTTON");

		AssertTextEquals(
			locator1 = "ZendeskHomepage#MESSAGE",
			value1 = "Your request was successfully submitted.");

		Click(locator1 = "ZendeskHomepage#MESSAGE_DISMISS_LINK");
	}

	@prose = "I should be able to view a ticket with the following details ${table}"
	macro view {
		var RawTable rawTable = new RawTable("${table}");

		for (var row : table "${rawTable}") {
			var field = "${row[0]}";
			var value = "${row[1]}";

			if ("${field}" == "Subject") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_SUBJECT",
					value1 = "${value}");
			}
			else if ("${field}" == "Id") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_ID",
					value1 = "${value}");
			}
			else if ("${field}" == "Status") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_STATUS",
					value1 = "${value}");
			}
			else if ("${field}" == "Requester") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_REQUESTER",
					value1 = "${value}");
			}
			else if ("${field}" == "Created") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_CREATED",
					value1 = "${value}");
			}
			else if ("${field}" == "Last activity") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_LAST_ACTIVITY",
					value1 = "${value}");
			}
			else if ("${field}" == "Organization") {
				AssertSelectedLabel(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_ORGANIZATION",
					value1 = "${value}");
			}
			else if ("${field}" == "Priority") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_PRIORITY",
					value1 = "${value}");
			}
			else if ("${field}" == "Product") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_PRODUCT",
					value1 = "${value}");
			}
			else if ("${field}" == "Sub-Status") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_SUB_STATUS",
					value1 = "${value}");
			}
			else if ("${field}" == "Component") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_COMPONENT",
					value1 = "${value}");
			}
			else if ("${field}" == "System Status") {
				AssertTextEquals(
					locator1 = "ZendeskTicketEndUserView#TICKET_DETAILS_SYSTEM_STATUS",
					value1 = "${value}");
			}
			else {
				fail("No such ticket field ${field}");
			}
		}
	}

	@prose = "I should be able to view a ticket comment written by ${userName} containing the text ${commentContent}"
	macro viewComment {
		AssertTextEquals(
			locator1 = "ZendeskTicketEndUserView#TICKET_COMMENT_AUTHOR",
			value1 = "${userName}",
			key_commentContent = "${commentContent}");

		AssertTextEquals(
			locator1 = "ZendeskTicketEndUserView#TICKET_COMMENT_CONTENT",
			value1 = "${commentContent}",
			key_commentContent = "${commentContent}");
	}
}